cmake_minimum_required(VERSION 3.30)

project(libneurosdk VERSION 0.2.1)

include(GNUInstallDirs)

option(NEURO_BUILD_STATIC "Build the library statically" ON)

execute_process(
	COMMAND git rev-parse HEAD
	WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}
	OUTPUT_VARIABLE LIB_BUILD_HASH
	OUTPUT_STRIP_TRAILING_WHITESPACE
	ERROR_QUIET
)
if(NOT LIB_BUILD_HASH)
	set(LIB_BUILD_HASH "unknown")
endif()

set(SOURCES ${CMAKE_CURRENT_SOURCE_DIR}/src/neurosdk.c)

if(NEURO_BUILD_STATIC)
	add_library(${PROJECT_NAME} STATIC ${SOURCES})
else()
	add_library(${PROJECT_NAME} SHARED ${SOURCES})
endif()

target_include_directories(${PROJECT_NAME} PUBLIC
	$<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/include>
	$<INSTALL_INTERFACE:${CMAKE_INSTALL_INCLUDEDIR}>
)
target_include_directories(${PROJECT_NAME} PRIVATE
	${CMAKE_CURRENT_SOURCE_DIR}/vendor
)

target_compile_definitions(${PROJECT_NAME} PRIVATE
	LIB_VERSION=${PROJECT_VERSION}
	LIB_BUILD_HASH=${LIB_BUILD_HASH}
)

configure_file(
	${CMAKE_CURRENT_SOURCE_DIR}/neurosdk.pc.in
	${CMAKE_CURRENT_BINARY_DIR}/neurosdk.pc
	@ONLY
)

install(TARGETS ${PROJECT_NAME}
	ARCHIVE DESTINATION ${CMAKE_INSTALL_LIBDIR}
	LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR}
	RUNTIME DESTINATION ${CMAKE_INSTALL_BINDIR}
)
install(FILES ${CMAKE_CURRENT_SOURCE_DIR}/include/neurosdk.h
	DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}
)
install(FILES ${CMAKE_CURRENT_BINARY_DIR}/neurosdk.pc
	DESTINATION ${CMAKE_INSTALL_LIBDIR}/pkgconfig
)
